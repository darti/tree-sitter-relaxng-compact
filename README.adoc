= Asciidoc-rs

== Relax NG

https://relaxng.org/spec-20011203.html#IDAODZR

=== Compact

https://www.oasis-open.org/committees/relax-ng/compact-20021121.html

[source]
----


topLevel	  ::=  	decl* (pattern | grammarContent*)
decl	  ::=  	"namespace" identifierOrKeyword "=" namespaceURILiteral
| "default" "namespace" [identifierOrKeyword] "=" namespaceURILiteral
| "datatypes" identifierOrKeyword "=" literal
pattern	  ::=  	"element" nameClass "{" pattern "}"
| "attribute" nameClass "{" pattern "}"
| pattern ("," pattern)+
| pattern ("&" pattern)+
| pattern ("|" pattern)+
| pattern "?"
| pattern "*"
| pattern "+"
| "list" "{" pattern "}"
| "mixed" "{" pattern "}"
| identifier
| "parent" identifier
| "empty"
| "text"
| [datatypeName] datatypeValue
| datatypeName ["{" param* "}"] [exceptPattern]
| "notAllowed"
| "external" anyURILiteral [inherit]
| "grammar" "{" grammarContent* "}"
| "(" pattern ")"
param	  ::=  	identifierOrKeyword "=" literal
exceptPattern	  ::=  	"-" pattern
grammarContent	  ::=  	start
| define
| "div" "{" grammarContent* "}"
| "include" anyURILiteral [inherit] ["{" includeContent* "}"]
includeContent	  ::=  	define
| start
| "div" "{" includeContent* "}"
start	  ::=  	"start" assignMethod pattern
define	  ::=  	identifier assignMethod pattern
assignMethod	  ::=  	"="
| "|="
| "&="
nameClass	  ::=  	name
| nsName [exceptNameClass]
| anyName [exceptNameClass]
| nameClass "|" nameClass
| "(" nameClass ")"
name	  ::=  	identifierOrKeyword
| CName
exceptNameClass	  ::=  	"-" nameClass
datatypeName	  ::=  	CName
| "string"
| "token"
datatypeValue	  ::=  	literal
anyURILiteral	  ::=  	literal
namespaceURILiteral	  ::=  	literal
| "inherit"
inherit	  ::=  	"inherit" "=" identifierOrKeyword
identifierOrKeyword	  ::=  	identifier
| keyword
identifier	  ::=  	(NCName - keyword)
| quotedIdentifier
quotedIdentifier	  ::=  	"\" NCName
CName	  ::=  	NCName ":" NCName
nsName	  ::=  	NCName ":*"
anyName	  ::=  	"*"
literal	  ::=  	literalSegment ("~" literalSegment)+
literalSegment	  ::=  	'"' (Char - ('"' | newline))* '"'
| "'" (Char - ("'" | newline))* "'"
| '"""' (['"'] ['"'] (Char - '"'))* '"""'
| "'''" (["'"] ["'"] (Char - "'"))* "'''"
keyword	  ::=  	"attribute"
| "default"
| "datatypes"
| "div"
| "element"
| "empty"
| "external"
| "grammar"
| "include"
| "inherit"
| "list"
| "mixed"
| "namespace"
| "notAllowed"
| "parent"
| "start"
| "string"
| "text"
| "token"
----


=== Simple grammar

[source]
----


pattern	  ::=  	<element name="QName"> pattern+ </element>
| <element> nameClass pattern+ </element>
| <attribute name="QName"> [pattern] </attribute>
| <attribute> nameClass [pattern] </attribute>
| <group> pattern+ </group>
| <interleave> pattern+ </interleave>
| <choice> pattern+ </choice>
| <optional> pattern+ </optional>
| <zeroOrMore> pattern+ </zeroOrMore>
| <oneOrMore> pattern+ </oneOrMore>
| <list> pattern+ </list>
| <mixed> pattern+ </mixed>
| <ref name="NCName"/>
| <parentRef name="NCName"/>
| <empty/>
| <text/>
| <value [type="NCName"]> string </value>
| <data type="NCName"> param* [exceptPattern] </data>
| <notAllowed/>
| <externalRef href="anyURI"/>
| <grammar> grammarContent* </grammar>
param	  ::=  	<param name="NCName"> string </param>
exceptPattern	  ::=  	<except> pattern+ </except>
grammarContent	  ::=  	start
| define
| <div> grammarContent* </div>
| <include href="anyURI"> includeContent* </include>
includeContent	  ::=  	start
| define
| <div> includeContent* </div>
start	  ::=  	<start [combine="method"]> pattern </start>
define	  ::=  	<define name="NCName" [combine="method"]> pattern+ </define>
method	  ::=  	choice
| interleave
nameClass	  ::=  	<name> QName </name>
| <anyName> [exceptNameClass] </anyName>
| <nsName> [exceptNameClass] </nsName>
| <choice> nameClass+ </choice>
exceptNameClass	  ::=  	<except> nameClass+ </except>
----

== Links

- https://tdg.docbook.org/tdg/5.2/
